managed;
strict ( 2 );
with draft;

define behavior for zr_travel_2303 alias Travel
implementation in class zbp_r_travel_2303 unique
persistent table ztravel_log_a
draft table ztravel_log_d
lock master
total etag LastChangedAt
authorization master ( global, instance )
etag master LocalLastChangedAt
{
  create ( precheck );
  update ( precheck );
  delete;
  association _Booking
  { create ( features : instance, authorization : update );
    with draft; }

  field ( numbering : managed, readonly ) TravelUUID;
  field ( readonly ) TravelID, OverallStatus, TotalPrice,
  LocalCreatedAt, LocalCreatedBy, LocalLastChangedAt, LocalLastChangedBy;
  field ( mandatory ) CustomerID, AgencyID, BeginDate, EndDate;
  field ( features : instance ) BookingFee;

  action ( features : instance, authorization : update ) acceptTravel result [1] $self;
  action ( features : instance, authorization : update ) rejectTravel result [1] $self;
  action ( features : instance, authorization : update ) deductDiscount result [1] $self;
  internal action reCalcTotalPrice;

  determination setTravelNumber on save { create; }
  determination setStatusToOpen on modify { create; }
  determination calculateTotalPrice on modify
  { create;
    field BookingFee, CurrencyCode; }

  validation validateAgency on save { create; field AgencyID; }
  validation validateCustomer on save { create; field CustomerID; }
  validation validateDates on save { create; field BeginDate, EndDate; }

  draft action Resume with additional implementation;
  draft action Edit;
  draft action Activate optimized;
  draft action Discard;

  draft determine action Prepare
  {
    validation validateAgency;
    validation validateCustomer;
    validation validateDates;
    validation Booking~validateCustomer;
    validation Booking~validateConnection;
  }

  mapping for ztravel_log_a
    {
      TravelUUID         = travel_uuid;
      TravelID           = travel_id;
      CustomerID         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee        = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      LocalCreatedBy     = local_created_by;
      LocalCreatedAt     = local_created_at;
      LocalLastChangedBy = local_last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
    }

}

define behavior for zr_booking_2303 alias Booking
implementation in class zbp_r_booking_2303 unique
persistent table zbooking_log_a
draft table zbooking_log_d
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;

  association _Travel { with draft; }
  field ( numbering : managed, readonly ) BookingUUID;
  field ( readonly ) TravelUUID, BookingID, BookingDate, LocalLastChangedAt;
  field ( mandatory ) CustomerID, AirlineID, ConnectionID, FlightDate;

  determination setBookingNumber on save { create; }
  determination setBoodingDate on save { create; }
  determination calculateTotalPrice on modify { create; field FlightPrice, CurrencyCode; }

  validation validateCustomer on save { create; field AirLineID; }
  validation validateConnection on save { create; field AirLineID, ConnectionID, FlightDate; }

  mapping for zbooking_log_a
    {
      AirLineID          = carrier_id;
      BookingDate        = booking_date;
      BookingID          = booking_id;
      BookingStatus      = booking_status;
      BookingUUID        = booking_uuid;
      ConnectionID       = connection_id;
      CurrencyCode       = currency_code;
      CustomerID         = customer_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      LocalLastChangedAt = local_last_changed_at;
      TravelUUID         = parent_uuid;
    }

}